<!DOCTYPE html>
<html lang="en">
<head>
	<title>ShaderToy Cloud Rendering</title>
	<meta charset="utf-8">
	<!-- <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0"> -->

	<style type="text/css">
	body{
		padding: 0;
		margin: 0;
		background-color: rgb(76, 162, 207);
	}
	canvas{
		display: block;
		/*transform: scale(2.0, 2.0);*/
		/*transform-origin: top left;*/
	}
	</style>
</head>
<body>
	<!-- Shaders -->
	<script id="screenProjectionVS" type="x-shader/x-vertex">
	uniform vec3 cameraNormal;
	varying vec3 worldCoord;

	const float near = 0.001 * 1.01;

	void main(void){
		//@! if position behind camera, flatten position to camera plane
		vec3 p = position;

		vec3 cameraToP = p - cameraPosition;
		float d = dot(cameraToP, cameraNormal);

		if(d < near){
			p = p - (d - near) * cameraNormal;
		}

		worldCoord = p + 0.5;
		gl_Position = (projectionMatrix * modelViewMatrix * vec4(p, 1.0));
	}
	</script>

	<script id="faceCoordinatesFS" type="x-shader/x-fragment">
	varying vec3 worldCoord;
	void main(void){
		gl_FragColor = vec4(worldCoord, 1.);
	}
	</script>

	<script id="raytraceCloudsFS" type="x-shader/x-fragment">
	uniform vec2 backfaceInvResolution;
	uniform sampler2D backfaceTexture;
	uniform sampler2D noiseTexture;

	uniform vec3 sunPosition;
	uniform float time;

	varying vec3 worldCoord;

	const bool debugLOD = false;
	const float transmittance = 2.8;//1.3
	const float noiseScale = 3.0;
	const vec3 shadeColor = vec3(130./255., 130./255., 130./255.) * 1.8;
	const vec3 diffuseColor = vec3(255./255., 254./255., 240./255.);

	//iq's magic 3d texture noise
	float noise( in vec3 x ){
		vec3 p = floor(x);
		vec3 f = fract(x);
		f = f*f*(3.0-2.0*f);
		
		vec2 uv = (p.xy+vec2(37.0,17.0)*p.z) + f.xy;
		vec2 rg = texture2D( noiseTexture, (uv+ 0.5)/256.0, -100.0 ).yx;
		return mix( rg.x, rg.y, f.z );
	}

	float map6(in vec3 p){
		vec3 q = p;
		float f;
		f  = 0.5000*noise( q ); q = q*2.02;
		f += 0.2500*noise( q ); q = q*2.03;
		f += 0.12500*noise( q ); q = q*2.01;
		f += 0.06250*noise( q ); q = q*2.02;
		f += 0.03125*noise( q ); q = q*2.03;
		f += 0.015625*noise( q );
		return f * f * f;
	}

	float map5(in vec3 p){
		vec3 q = p;
		float f;
		f  = 0.5000*noise( q ); q = q*2.02;
		f += 0.2500*noise( q ); q = q*2.03;
		f += 0.12500*noise( q ); q = q*2.01;
		f += 0.06250*noise( q ); q = q*2.02;
		f += 0.03125*noise( q );
		return f * f * f;
	}

	float map4(in vec3 p){
		vec3 q = p;
		float f;
		f  = 0.5000*noise( q ); q = q*2.02;
		f += 0.2500*noise( q ); q = q*2.03;
		f += 0.12500*noise( q ); q = q*2.01;
		f += 0.06250*noise( q );
		return f * f * f;
	}

	float map3(in vec3 p){
		vec3 q = p;
		float f;
		f  = 0.5000*noise( q ); q = q*2.02;
		f += 0.2500*noise( q ); q = q*2.03;
		f += 0.12500*noise( q );
		return f * f * f;
	}

	float map2(in vec3 p){
		vec3 q = p;
		float f;
		f  = 0.5000*noise( q ); q = q*2.02;
		f += 0.2500*noise( q );
		return f * f * f;
	}

	vec4 raymarchTransmittance(in vec3 rayStart, in vec3 rayDir, in float maxLen){
		float scd = distance(cameraPosition, rayStart);
		float denMultiplier = (sin(time * 0.2)*0.8 + 2.0)*.5;
		const float denOffset = 0.1;
		vec3 noiseOffset = vec3(time * .07, 0.0, time * .05);


		float stepSize = 0.01;
		float tm = 1.0;
		float t = 0.;

		vec3 col = vec3(0.0);

		vec3 pos, uCol, debugCol;
		float den, df, dtm, alpha, shadeFactor;

                            //for(int i = 0; i < N; i++){
#define POS()                   pos = t * rayDir + rayStart;
#define DENSITY(MAP)            den = MAP(pos*noiseScale + noiseOffset)*denMultiplier - denOffset
                                //if(den > 0.01){
#define HANDLE_TRANSMITTANCE()      dtm = exp(-transmittance * den); tm *= dtm
#define SHADE_FACTOR()              shadeFactor = 1. - dot(pos, sunPosition)
#define COLOR()                     uCol = mix(diffuseColor, shadeColor, shadeFactor) / (pow(shadeFactor + 0.4, .4) * 1.0)
#define DEBUG_COLOR()               if(debugLOD){ uCol = uCol * debugCol; }
#define ACCUMULATE_COLOR()          col += (1.0 - dtm) * uCol * tm
                                //}
#define STEP(MIN_STEP, SCALE)   df = (scd *.25 + t); stepSize = max(MIN_STEP, df*SCALE); t += stepSize
#define RETURN()                alpha = 1.0 - tm; return vec4(col / max(alpha, 0.01), alpha)
#define EXIT_CONDITION()        if(t >= maxLen || tm < 0.01){RETURN();}
                            //}

//full march loop
#define MARCH(N, MAP, MIN_STEP, STEP_SCALE) for(int i = 0; i < N; i++){ POS(); DENSITY(MAP); if(den > 0.01){ HANDLE_TRANSMITTANCE(); SHADE_FACTOR(); COLOR(); DEBUG_COLOR(); ACCUMULATE_COLOR(); } STEP(MIN_STEP, STEP_SCALE); EXIT_CONDITION(); }
		

		const float d = 0.6;
		debugCol = vec3(d, d, 1.0);//blue
		MARCH(10, map3, 0.01, 0.00);
		
		debugCol = vec3(d, 1.0, d);//green
		MARCH(40, map6, 0.01, 0.00);

		debugCol = vec3(1.0, d, d);//red
		MARCH(20, map5, 0.01, 0.00);

		debugCol = vec3(1.0, 1.0, d);//yellow
		MARCH(20, map3, 0.02, 0.00);

		debugCol = vec3(1.0, d, 1.0);//pink/purple
		MARCH(100, map2, 0.01, 0.00);

		RETURN();
	}

	void main(void){
		vec4 bfsamp = texture2D(backfaceTexture, gl_FragCoord.xy * backfaceInvResolution);

		//@!tmp
		// gl_FragColor = vec4(bfsamp.xyz, 1.0);
		// return;

		//reject null backface texture sample
		//this is where the current pixel doesn't line up exactly with a backface pixel
		if(bfsamp.a < 0.01){
			gl_FragColor = vec4(0);
			return;
		}

		vec3 rayStart = worldCoord;
		vec3 rayEnd = bfsamp.xyz;
		vec3 rayVec = rayEnd - rayStart;
		float rayLen = length(rayVec);
		vec3 rayDir = rayVec / rayLen;

		vec4 col = raymarchTransmittance(rayStart, rayDir, rayLen);

		//sun glare
		float dotSun = dot(sunPosition, rayDir);

		if(dotSun < 0.0){
			dotSun = -dotSun * 0.3;//reduce reflection intensity
			col.xyz += 0.4*vec3(0.99,0.99,0.90) * dotSun;
		}else{
			col.xyz += 0.4*vec3(0.99,0.99,0.90) * dotSun * dotSun * dotSun / max(col.a, 0.01);
		}


		gl_FragColor = col;
	}
	</script>

	<!-- libraries -->
	<script type="text/javascript" src="js/three/three.min.js"></script>
	<script type="text/javascript" src="js/three/controls/OrbitControls.js"></script>
	<script type="text/javascript" src="js/three/shaders/SkyShader.js"></script>
	<!-- app -->
	<script type="text/javascript" src="js/url-params.js"></script>
	<script type="text/javascript" src="js/shader-tools.js"></script>
	<script type="text/javascript" src="js/app.js"></script>
</body>
</html>